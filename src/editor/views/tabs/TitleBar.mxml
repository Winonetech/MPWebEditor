<?xml version="1.0" encoding="utf-8"?>
<s:BorderContainer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				   xmlns:s ="library://ns.adobe.com/flex/spark" 
				   xmlns:mx="library://ns.adobe.com/flex/mx" 
				   xmlns:tabs="editor.views.tabs.*"
				   borderAlpha="0" minWidth="68"
				   creationComplete="TB_creationCompleteHandler(event)"
				   resize="TB_resizeHandler(event)" 
				   height="30" backgroundAlpha="0">
	
	<s:states>
		<s:State name="normal"/>
		<s:State name="much"/>
	</s:states>
	<s:HGroup id="TC" gap="5">
		<s:HGroup id="HG" gap="5" updateComplete="HG_updateCompleteHandler(event)" resize="HG_resizeHandler(event)">
			<tabs:TitleTab id="tabLayout" width="40" label="布局" layoutCloseVisible="false"/>
		</s:HGroup>
		
		<s:ComboBox id="comboBox" includeIn="much" dataProvider="{dataArrs}"
					skinClass="editor.skins.TitleBarComboboxSkin"
					change="combobox1_changeHandler(event)"/>
	</s:HGroup>
	<fx:Script>
		<![CDATA[
			import editor.core.MDConfig;
			import editor.core.MDProvider;
			import editor.core.MDVars;
			import editor.core.ed;
			import editor.views.PageContent;
			import editor.views.CanvasContent;
			import editor.views.Debugger;
			import editor.vos.Sheet;
			
			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;
			import mx.events.ResizeEvent;
			
			import spark.events.IndexChangeEvent;
			
			
			
			private function sheet2Tab($sheet:Sheet):TitleTab
			{
				var temp:TitleTab;
				for (var i:int = 1; i < HG.numElements; i++)
				{
					temp = HG.getElementAt(i) as TitleTab	
					if ($sheet.id == idObj[temp])
					{
						return temp; 
					}
				}
				for (var j:int; j < dataArrs.length; j++)
				{
					temp = dataArrs[j];
					if ($sheet.id == idObj[temp])
					{
						return temp;
					}
				}
				return null;
			}
			
			private function tab2Sheet($tab:TitleTab):Sheet
			{
				return MDProvider.instance.program.sheets[idObj[$tab]];
			}
			
			private function updateBackgroundColor():void
			{
				var temp:TitleTab;
				tabLayout.setStyle("backgroundColor", 0xffffff);
				for (var i:int = 1; i < HG.numElements; i++)
				{
					temp = HG.getElementAt(i) as TitleTab;
					if (idObj[temp] != idObj[selected])
					{
						temp.button.visible = false;
						temp.setStyle("backgroundColor", 0xffffff);
					}
					else
					{
						temp.button.visible = true;
						temp.setStyle("backgroundColor", 0);
					}
				}
			}
			
			protected function combobox1_changeHandler(event:IndexChangeEvent):void
			{
				MDConfig.instance.selectedSheet = null;
				var temp:TitleTab = comboBox.selectedItem as TitleTab;
				var tempWidth:Number = HG.width;
				while (temp.width + tempWidth > width - 50)
				{
					var removed:TitleTab = HG.removeElementAt(HG.numElements - 1) as TitleTab;
					var tempNamed:Number = removed.named.width >= 43 ? 43 : removed.named.width;
					tempWidth -= removed.width + tempNamed - 38;
					dataArrs.addItem(removed);
				}
				dataArrs.removeItemAt(comboBox.selectedIndex);
				HG.addElement(temp);
				selected = temp;
			}
			
			private function dataArrs_Pop():void
			{
				var temp:TitleTab;
				var tempWidth:Number = HG.width;
				var tempNamed:Number;
				var l:int;
				if (dataArrs.length != 0)
				{
					l = dataArrs.length - 1;
					for (var i:int = l; i >= 0; i--)
					{
						if (dataArrs[i].width + tempWidth < width - 50)
						{
							tempNamed = dataArrs[i].named.width >= 43 ? 43 : dataArrs[i].named.width;
							temp = HG.addElement(dataArrs[i]) as TitleTab;
							tempWidth += dataArrs[i].width + tempNamed - 1;
							dataArrs.removeItemAt(i);
						}
					}
				}
				if (HG.numElements > 1 && selected == null) 
				{
					selected = HG.getElementAt(HG.numElements - 1) as TitleTab; 
				}
				if (selected != tabLayout) updateBackgroundColor();
			}
			
			private function dataArrs_Push():void
			{
				var tempTab:TitleTab;
				var tempNamed:Number;
				var temp:Number = HG.width;
				while (HG.numElements > 2 && temp > width - 50)
				{
					currentState = "much";
					tempTab = HG.getElementAt(HG.numElements - 2) as TitleTab;
					if (tempTab == selected)
					{
						if (HG.getElementIndex(tempTab) < HG.numElements - 1)
							tempTab = HG.getElementAt(HG.getElementIndex(tempTab) + 1) as TitleTab;
						else if (HG.numElements > 3)
							tempTab = HG.getElementAt(HG.numElements - 3) as TitleTab;
						else break;
					}
					tempNamed = tempTab.named.width >= 43 ? 43 : tempTab.named.width;
					temp -= tempTab.width + tempNamed - 38;
					dataArrs.addItem(tempTab);
					HG.removeElementAt(HG.getElementIndex(tempTab));
				}
			}
			
			protected function TB_resizeHandler(event:ResizeEvent):void
			{
				state = event.oldWidth > width ? "open" : "close";
				isResize = true;
				HG.dispatchEvent(new ResizeEvent(ResizeEvent.RESIZE));
			}
			
			protected function HG_resizeHandler(event:ResizeEvent):void
			{
				if (state == "close") dataArrs_Pop();
				else
				{
					if (isResize || (count > 1 && count % 2 == 1))
						dataArrs_Push();
					if (!isResize) count++;
					else isResize = false;
				}
			}
			
			/**
			 * @private
			 */
			public function addTitle(sheet:Sheet):void
			{
				var title:TitleTab = new TitleTab;
				if (repeatById(sheet.id))
				{
					var temp:TitleTab = HG.addElement(title) as TitleTab;
					idObj[temp] = sheet.id;
				}
				selected = sheet2Tab(sheet);
			}
			
			/**
			 * @private
			 */
			private function repeatById($id:String):Boolean
			{
				for each (var id:String in idObj)
				if (id == $id) return false;
				return true;
			}
			
			private function repeatByTab($tab:TitleTab):Boolean
			{
				for each (var tab:TitleTab in dataArrs)
				if (tab == $tab) return false;
				return true;	
			}
			
			/**
			 * 
			 * 当前正在编辑的页面选项卡。
			 * 
			 */
			
			[Bindable]
			public function get selected():TitleTab
			{
				return ed::selected;
			}
			
			/**
			 * @private
			 */
			public function set selected(value:TitleTab):void
			{
				var temp:Sheet = tab2Sheet(value);
				ed::selected = value;
				if (temp)
				{
					MDConfig.instance.editingSheet = MDConfig.instance.selectedSheet = temp;
					if (!repeatByTab(value))
					{
						var tempWidth:Number = HG.width;
						var tempNamed:Number;
						var removed:TitleTab;
						while (value.width + tempWidth > width - 50)
						{
							removed = HG.removeElementAt(HG.numElements - 1) as TitleTab;
							dataArrs.addItem(removed);
							tempNamed = removed.named.width >= 43 ? 43 : removed.named.width;
							tempWidth -= removed.width + tempNamed - 38;
						}
						dataArrs.removeItemAt(dataArrs.getItemIndex(value));
						HG.addElement(value);
					}
					updateBackgroundColor();
				}
			}
			
			protected function HG_updateCompleteHandler(event:FlexEvent):void
			{
				if (dataArrs.length == 0) currentState = "normal";
			}
			
			protected function TB_creationCompleteHandler(event:FlexEvent):void
			{
				tabLayout.addEventListener(MouseEvent.CLICK, tabLayout_clickHandler, true);
			}
			
			
			protected function tabLayout_clickHandler(event:MouseEvent):void
			{
				config.selectedSheet = config.editingSheet = null;
				config.selectedComponent = null;
				var temp:TitleTab;
				ed::selected = tabLayout;
				for (var i:int; i < HG.numElements; i++)
				{
					temp = HG.getElementAt(i) as TitleTab;
					temp.button.visible = false;
					temp.setStyle("backgroundColor", 0xffffff);
				}
				tabLayout.setStyle("backgroundColor", 0);
				
				new PageContent().update();
				
				event.stopImmediatePropagation();
			}
			
			
			
			/**
			 * @private
			 */
			ed var selected:TitleTab;
			
			[Bindable]
			public var dataArrs:ArrayCollection = new ArrayCollection; 
			
			public var idObj:Object = {};
			
			public var state:String;
			
			private var oldWidth:Number;
			
			private var count:int = 0;
			
			private var isResize:Boolean;
			
			public function get isLayoutOpened():Boolean
			{
				return selected == tabLayout;
			}
			/**
			 * @private
			 */
			private static function get vars():MDVars
			{
				return MDVars.instance;
			}

			
			/**
			 * @private
			 */
			private static function get config():MDConfig
			{
				return MDConfig.instance;
			}
			
			
		]]>
	</fx:Script>
	
</s:BorderContainer>
